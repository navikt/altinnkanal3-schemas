buildscript {
    ext {
        avro_version = '1.10.1'
    }
}

plugins {
    id "com.github.davidmc24.gradle.plugin.avro" version "1.1.0"
}

group 'no.nav.altinnkanal3.avro'
version "${project.version}"

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven {
        name 'Confluent maven repo'
        url 'https://packages.confluent.io/maven/'
    }
    jcenter()

    String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubPassword")
    if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or githubPassword")
    maven {
        name 'github-package-registry-navikt'
        url 'https://maven.pkg.github.com/navikt/maven-release/'
        credentials {
            username = 'x-access-token'
            password = token
        }
    }
    // internal - local repos
    maven {
        name 'internal-mirror-github-navikt'
        url 'https://repo.adeo.no/repository/github-package-registry-navikt/'
    }
    maven {
        name 'nexus-internal-release'
        url 'https://repo.adeo.no/repository/maven-releases/'
    }
}

apply plugin: 'maven-publish'

dependencies {
    implementation "org.apache.avro:avro:$avro_version"
}

task printVersion() {
    doLast {
        println(version)
    }
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/navikt/altinnkanal3-schemas")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            pom {
                name.set("altinnkanal3-schemas")
                description.set("Topic skjema produsert av altinnkanal3")
                url.set("https://github.com/navikt/altinnkanal3-schemas")
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/licenses/MIT")
                    }
                }

                scm {
                    connection.set("scm:git:https://github.com/navikt/altinnkanal3-schemas.git")
                    developerConnection.set("scm:git:https://github.com/navikt/altinnkanal3-schemas.git")
                    url.set("https://github.com/navikt/altinnkanal3-schemas")
                }
            }
            from components.java
        }
    }
}
